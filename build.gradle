apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

repositories {
    jcenter()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def defaultEncoding = 'UTF-8'
compileJava {
    options.encoding = defaultEncoding
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group = 'com.os.operando.meteoroid'
version = '1.2.0'

def siteUrl = 'https://github.com/operando/Meteoroid'
def gitUrl = 'https://github.com/operando/Meteoroid.git'
def issueUrl = 'https://github.com/operando/Meteoroid/issues'

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'meteoroid'
            version version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'meteoroid'
        desc = 'Uploading files to Slack.'
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        labels = ['java']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'meteoroid'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'operando'
                        name 'Shinobu Okano'
                        email 'okanoshinobu8@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/operando/Meteoroid.git'
                    developerConnection 'https://github.com/operando/Meteoroid.git'
                    url siteUrl

                }
            }
        }
    }
}