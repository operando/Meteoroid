apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

repositories {
    jcenter()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def defaultEncoding = 'UTF-8'
compileJava {
    options.encoding = defaultEncoding
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp:okhttp:2.5.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group = 'com.os.operando.meteor'
version = '1.0.1'

def siteUrl = 'https://github.com/operando/Meteor'
def gitUrl = 'https://github.com/operando/Meteor.git'
def issueUrl = 'https://github.com/operando/Meteor/issues'

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'meteor'
            version version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'meteor'
        desc = 'Uploading files to Slack.'
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        labels = ['java']
        publicDownloadNumbers = true
    }
}